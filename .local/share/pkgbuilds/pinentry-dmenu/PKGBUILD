# Maintainer: Ratakor <ratakor@disroot.org>

pkgname=pinentry-dmenu
pkgver=r57.460fde7
pkgrel=1
pkgdesc="pinentry program based on dmenu"
arch=('x86_64')
url="https://github.com/ratakor/$pkgname"
license=('GPL')
depends=('libx11' 'libxft' 'libxinerama' 'freetype2')
provides=("$pkgname")
conflicts=("$pkgname")
source=("git+$url")
sha256sums=('SKIP')

pkgver() {
	cd "$pkgname"
	printf 'r%s.%s' \
		"$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$srcdir/$pkgname"
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
	make DESTDIR="$pkgdir" PREFIX="/usr" install
}
